set PREVIEW_MESSAGE "CartInstall\n \nA fork of d0k3's GodMode9\nby aspargas2"
set PREVIEW_MODE $[PREVIEW_MESSAGE]

if ask "Would you like to view the readme?"
    textview V:/README_internal.md
    set PREVIEW_MODE $[PREVIEW_MESSAGE]
end
    
@CartInstall
cartinit -o -s
if not find C:/*.trim.3ds ROM_PATH
    if ask "Unable to detect 3DS cartridge.\n \nTry again?\nChoosing no will power off."
        goto CartInstall
    else
        poweroff
    end
end

if not imgmount $[ROM_PATH]
    echo "Unable to mount cartridge image.\nThis should not happen.\nYour cartridge may be corrupted.\nThe system will now power off."
end

if not find G:/content0.game.app CONTENT0_PATH
    echo "Cartridge image mounted, but unable to find content0.\nThis should not happen.\nYour cartridge may be corrupted.\nThe system will now power off."
    poweroff
end

@SDCheck
if not isdir 0:
    if ask "Could not detect SD card.\n \nTry again?\nChoosing no will power off."
        goto SDCheck
    else
        poweroff
    end
end

if not find A:/dbs/title.db TITLEDB
    echo "title.db in virtual SD data mount does not exist.\nPlease open the eShop or install any SD title,\nthen run CartInstall again."
    poweroff
end

if not find 1:/dbs/ticket.db TICKDB
    echo "ticket.db in CTRNAND does not exist.\nThis should not happen.\nThe system will now power off."
    poweroff
end

if not allow A:/
    echo "Write permissions denied.\nThe system will now power off."
    poweroff
end

fget -e $[CONTENT0_PATH]@118:4 TID_LOW
set TID_LOW_DIR A:/title/00040000/$[TID_LOW]
set CONTENT_DIR $[TID_LOW_DIR]/content

if isdir $[CONTENT_DIR]
    if ask "Some data alredy exists on the SD for\nthe game on the gamecart!\n \nOverwrite it?\nChoosing no will power off."
        rm $[CONTENT_DIR]
    else
        poweroff
    end
end

set WORKDIR 0:/CartInstall
mkdir -o -s $[WORKDIR]

if not decrypt -o -h -f $[CONTENT0_PATH] $[CONTENT_DIR]/00000000.app
    goto CopyFail
end

if find G:/content1.manual.app CONTENT1_PATH
    if not decrypt -o -h -f $[CONTENT1_PATH] $[CONTENT_DIR]/00000001.app
        goto CopyFail
    end
end

if find G:/content2.dlp.app CONTENT2_PATH
    if not decrypt -o -h -f $[CONTENT2_PATH] $[CONTENT_DIR]/00000002.app
        goto CopyFail
    end
end

imgumount

mkdir $[CONTENT_DIR]/cmd
if not buildmds -t -c $[CONTENT_DIR]
    echo "Failed to build metadata.\n \nThis should not happen; please report this as\nit is likely a bug."
    goto ContentRemove
end

cp -w $[TICKDB] $[WORKDIR]/ticket.db.bak
cp -w $[TITLEDB] $[WORKDIR]/title.db.bak

if not insttmd $[CONTENT_DIR]/00000000.tmd
    echo "Failed to generate and install\ntitle info entry and ticket.\n \nThis should not happen; please report this as\nit is likely a bug.\n \nThe SD title.db and CTRNAND ticket.db will\nnow be restored to their original state."
    cp -w $[WORKDIR]/ticket.db.bak $[TICKDB]
    cp -w $[WORKDIR]/title.db.bak $[TITLEDB]
    goto ContentRemove
end

fget $[CONTENT_DIR]/00000000.tmd@19B:4 SAVE_SIZE
if not chk $[SAVE_SIZE] 00000000
    set SAVE_PATH $[TID_LOW_DIR]/data/00000001.sav 
    if not exist $[SAVE_PATH]
        mkdir -o -s $[TID_LOW_DIR]/data
        fdummy $[SAVE_PATH] $[SAVE_SIZE]
        fill $[SAVE_PATH]@0:20 00
    end
end

if ask "Done!\n \nWould you like to install another cartridge game now?\nChoosing no will reboot."
    echo "Please swap cartridges now."
    goto CartInstall
else
    reboot
end

@CopyFail
echo "Content decryption failed.\nThis could be due to not having enough free space\non the SD, or due to the user cancelling the copy."
@ContentRemove
echo "The system will now remove any gamedata created\nin this session and power off."
rm -o -s $[CONTENT_DIR]
poweroff